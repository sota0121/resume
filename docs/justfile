[private]
default:
  @just --list


# Install dependencies
[group('setup')]
install-deps:
  # https://www.npmjs.com/package/md-to-pdf
  @npm i -g md-to-pdf

# Install dependencies for CI
[group('ci')]
install-deps-ci:
  npm ci
  npm run install:chrome


# Build a PDF for specified language jp by default. (Usage: just build-pdf jp|en false|true. Please set ci=true on CI)
[no-cd]
[group('pdf')]
build-pdf lang="jp" ci="false":
  #!/bin/bash
  # Verification
  if [ {{lang}} != "jp" ] && [ {{lang}} != "en" ]; then
    echo "Invalid language. Use 'jp' or 'en'."
    exit 1
  fi

  # Run md-to-pdf
  echo "Building PDF for {{lang}}..."
  if [ {{ci}} == "true" ]; then
    echo "Running in CI mode"
    # Run md-to-pdf with CI settings
    npx md-to-pdf "{{justfile_directory()}}/md/{{lang}}/RESUME.md" --config-file {{justfile_directory()}}/md/{{lang}}/pdfconf/config.js --launch-options '{ "args": ["--no-sandbox"] }'
  else
    echo "Running in local mode"
    # Run md-to-pdf with local settings
    npx md-to-pdf "{{justfile_directory()}}/md/{{lang}}/RESUME.md" --config-file {{justfile_directory()}}/md/{{lang}}/pdfconf/config.js
  fi

# Build the PDF for all languages (Usage: just build-pdf-all false|true. Please set ci=true on CI)
[no-cd]
[group('pdf')]
[confirm("Are you sure you want to build the PDF for all languages? (y/n)")]
build-pdf-all ci="false":
  @echo "build pdf in all languages"
  @just build-pdf jp {{ci}}
  @just build-pdf en {{ci}}

